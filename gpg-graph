#!/usr/bin/sbcl --script
;; -*- mode:lisp -*-
;;
;; gpg-graph - Draw web of trust graphs of a GnuPG keyring
;;
;; Author: Teemu Likonen <tlikonen@iki.fi>
;;
;; No restrictions for use: this program is placed in the public domain.
;;
;; This program is distributed in the hope that it will be useful, but
;; WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

(defpackage #:gpg-graph
  (:use #:cl))

(in-package #:gpg-graph)

(defvar *keys* (make-hash-table :test #'equal))

(defclass key ()
  ((user-id :accessor user-id :initform nil)
   (fingerprint :accessor fingerprint :initform nil)
   (certificates-from :accessor certificates-from :initform nil)
   (cerfificates-for :accessor certificates-for :initform nil)))

(defun get-create-key (key-id)
  (or (gethash key-id *keys*)
      (setf (gethash key-id *keys*) (make-instance 'key))))

(defun certificates-for-p (key cert)
  (member cert (certificates-for key)))

(defun list-of-certificates-from (key)
  (copy-list (certificates-from key)))

(defun add-certificates-from (key cert)
  (pushnew cert (certificates-from key)))

(defun add-certificates-for (key cert)
  (pushnew cert (certificates-for key)))

(defun remove-certificates-from (key cert)
  (setf (certificates-from key) (remove cert (certificates-from key))))

(defun split-colon-string (string)
  (loop :with items
        :with start := 0
        :for end := (position #\: string :start start)
        :do (push (subseq string start end) items)
            (if end
                (setf start (1+ end))
                (loop-finish))
        :finally (return (nreverse items))))

(defun string-replace (string search replace)
  (assert (plusp (length search)) (search)
          "Can't search for zero length SEARCH string.")
  (with-output-to-string (out)
    (loop :with length-search := (length search)
          :for pos := 0 :then (+ find length-search)
          :for find := (search search string :start2 pos)
          :if find :do
            (princ (subseq string pos find) out)
            (princ replace out)
          :else :do
            (princ (subseq string pos) out)
            (loop-finish))))

(defun escape-characters (string esc-chars esc)
  (with-output-to-string (out)
    (loop :for char :across string
          :do (when (find char esc-chars)
                (princ esc out))
              (princ char out))))

(defun prepare-user-id (string)
  ;; This should actually decode C language string.
  (setf string (string-replace string "\\x3a" ":"))
  (escape-characters string "\\" #\\))

(defun date-not-expired-p (expire)
  (let ((now (get-universal-time)))
    (cond ((string= expire ""))
          ((every #'digit-char-p expire)
           (> (+ (parse-integer expire)
                 2208988800)            ;1970-01-01T00:00:00Z
              now))
          ((and (>= (length expire) 15)
                (char= #\T (aref expire 8))
                (every #'digit-char-p (subseq expire 0 8))
                (every #'digit-char-p (subseq expire 9 15)))
           (let ((year (parse-integer (subseq expire 0 4)))
                 (month (parse-integer (subseq expire 4 6)))
                 (day (parse-integer (subseq expire 6 8)))
                 (hour (parse-integer (subseq expire 9 11)))
                 (min (parse-integer (subseq expire 11 13)))
                 (sec (parse-integer (subseq expire 13 15))))
             (> (encode-universal-time sec min hour day month
                                       year 0)
                now)))
          (t))))

(defun split-fingerprint (fingerprint)
  (loop :for i :from 4 :upto (length fingerprint) :by 4
        :collect (subseq fingerprint (- i 4) i)))

(defun main (&optional args)
  (clrhash *keys*)

  (with-open-stream
      (gpg (sb-ext:process-output
            (sb-ext:run-program "gpg" (list* "--batch" "--no-tty"
                                             "--with-colons"
                                             "--with-fingerprint"
                                             "--check-signatures"
                                             "--" args)
                                :search t :wait nil
                                :output :stream
                                :error *error-output*)))

    (loop :with key-id :with key :with expect
          :for line := (read-line gpg nil)
          :for fields := (if line (split-colon-string line))

          :while line :do
            (cond
              ((string= "pub" (nth 0 fields))
               (setf expect '(:fpr))
               (setf key-id (nth 4 fields))
               (setf key (get-create-key key-id)))

              ((string= "sub" (nth 0 fields))
               (setf expect nil))

              ((and (member :fpr expect)
                    (string= "fpr" (nth 0 fields)))
               (setf expect '(:uid))
               (setf (fingerprint key) (nth 9 fields)))

              ((and (member :uid expect)
                    (string= "uid" (nth 0 fields))
                    (not (user-id key)))
               (setf expect '(:sig))
               (setf (user-id key) (prepare-user-id (nth 9 fields))))

              ((and (member :sig expect)
                    (string= "sig" (nth 0 fields))
                    (plusp (length (nth 1 fields)))
                    (char= #\! (aref (nth 1 fields) 0))
                    (string/= key-id (nth 4 fields))
                    (date-not-expired-p (nth 6 fields)))
               (let ((cert (get-create-key (nth 4 fields))))
                 (add-certificates-from key cert)
                 (add-certificates-for cert key))))))

  (format t "~
digraph \"GnuPG key graph\" {
  overlap=scale;
  splines=true;
  node [shape=box];
")

  (loop :for key :being :each :hash-value :in *keys*
        :for user-id := (user-id key)
        :if user-id :do
          (format t "  \"~A\"~%    [label=\"~A\\l~A\"];~%"
                  (fingerprint key) user-id
                  (if (>= (length user-id) 60)
                      (format nil "~{~A~^ ~}\\l"
                              (split-fingerprint (fingerprint key)))
                      (format nil "~{~A ~A ~A ~A ~A~^ ...\\l... ~}\\r"
                              (split-fingerprint (fingerprint key)))))

          (loop :for cert :in (list-of-certificates-from key)
                :if (user-id cert) :do
                  (cond
                    ((certificates-for-p key cert)
                     (format t "    \"~A\" -> \"~A\" [dir=both];~%"
                             (fingerprint cert) (fingerprint key))
                     (remove-certificates-from cert key))
                    (t (format t "    \"~A\" -> \"~A\" [dir=forward];~%"
                               (fingerprint cert) (fingerprint key))))))

  (format t "}~%"))

#-interactive
(handler-case (main (rest sb-ext:*posix-argv*))
  (sb-int:simple-stream-error ()
    nil)
  (sb-sys:interactive-interrupt ()
    (terpri))
  (serious-condition (c)
    (format *error-output* "~&~A~%" c)))
